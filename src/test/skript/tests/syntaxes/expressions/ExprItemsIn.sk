test "items in (inventory)":
	set {_inventory} to a barrel inventory named "test"
	add stone to {_inventory}
	add stone named "bread" to {_inventory}
	add 100 of iron ingot to {_inventory}
	loop items in {_inventory}:
		if loop-value is stone:
			continue
		else if loop-value is iron ingot:
			continue
		else:
			assert true is false with "unexpected item in the inventory area: %loop-value%"
	set {_list::*} to items in {_inventory}
	assert size of {_list::*} is 4 with "size of items in failed: %size of {_list::*}%"
	assert {_list::1} is stone with "first item failed: %{_list::1}%"
	assert {_list::2} is stone named "bread" with "second item failed: %{_list::2}%"
	assert {_list::3} is 64 of iron ingot with "third item failed: %{_list::3}%"
	assert {_list::4} is 36 of iron ingot with "split fourth item failed: %{_list::4}%"
	remove stone from {_inventory}
	set {_list::*} to items in {_inventory}
	assert size of {_list::*} is 3 with "size of second items in failed: %size of {_list::*}%"
	assert {_list::1} is stone named "bread" with "new first item failed: %{_list::1}%"
	assert {_list::2} is 64 of iron ingot with "new second item failed: %{_list::2}%"
	assert {_list::3} is 36 of iron ingot with "new third item failed: %{_list::3}%"
